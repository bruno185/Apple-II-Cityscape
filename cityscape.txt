********************************************
Origianal code (BASIC, 1 line !!) 
********************************************

0HGR2:HCOLOR=6:HPLOT0,0:CALL62454:HCOLOR=4:FORN=0TO3:I=0:FORM=1TO1:J=I+INT(RND(1)*3)+2:J=J-((J>39)*(J-39)):T=INT(RND(1)*(4-N)*48)+N*48:FORX=ITOJ:FORY=TTO191STEP4-N:HPLOTX*7,YTOX*7+6,Y:I=J+1:M=J=39:NEXT:NEXT:NEXT:NEXT




********************************************
more readable Version
********************************************
0  HGR2 
10 HCOLOR= 6
20 HPLOT 0,0: CALL 62454
30 HCOLOR= 4: 
40 FOR N = 0 TO 3
50 I = 0
60 FOR M = 1 TO 1
70 J = I +  INT ( RND (1) * 3) + 2
80 J = J - ((J > 39) * (J - 39))
90 T =  INT ( RND (1) * (4 - N) * 48) + N * 48
100 FOR X = I TO J
110 FOR Y = T TO 191 STEP 4 - N
200 HPLOT X * 7,Y TO X * 7 + 6,Y
210 I = J + 1:
220 M = J = 39: 
230 NEXT: NEXT : NEXT : NEXT 


********************************************
With comments 
********************************************

      10 HGR2
      20 rem display graphic page 2 ($4000)
      30
      40 HCOLOR= 6
      50 rem color bleu
      60
      70 HPLOT 0,0:
      80 CALL 62454
      90 rem CLEAR current hires screen to HCOLOR of last dot ploted
     100
     110 HCOLOR= 4
     120 rem couleur noire
     130 rem
     140 rem loop #1 : N 
     150 FOR N = 0 TO 3
     160 rem 4 passes
     170
     180    I = 0
     190    rem I = start horizontally (X direction), reset reset to 0 on each pass
     200
     210    rem loop #2 : M (equ. while J <39) 
     220    FOR M = 1 TO 1
     230
     240        J = I +  INT (RND (1) * 3) + 2
     250        rem J = I + a number between 2 et 4
     260        rem J = end horozontally (X direction)
     280        J = J - ((J > 39) * (J - 39))
     290        rem equ. if J>39 then J = 39
     300        T =  INT ( RND (1) * (4 - N) * 48) + N * 48
     310        rem T between 0 and 191 (=hauteur )
                rem if N=0 : 0 < T < 192
                rem if N=1 : 48 < T < 192
                rem if N=2 : 96 < T < 192
                rem if N=3 : 144 < T < 192
     320        rem loop #3 : X (between I et J) 
     330        FOR X = I TO J : loop #4 
     340            FOR Y = T TO 191 STEP 4 - N : rem 4-N tighter lines on each pass
     350                HPLOT X * 7,Y TO X * 7 + 6,Y
     360                I = J + 1
     370                M = J = 39: rem M=1 if J=39 else M=0
     380            NEXT
     390        NEXT
     400    NEXT
     410 NEXT

 for narrower buildings:
 240 J = I +  INT ( RND (1) * 2) + 1

====================================================================
Optimised Version
====================================================================

 10  HGR2 
 40  HCOLOR= 6
 70  HPLOT 0,0:
 80  CALL 62454
 110  HCOLOR= 4
 130  REM 
 150  FOR N = 0 TO 3
 180 I = 0
 220  FOR M = 1 TO 1
 240 J = I +  INT ( RND (1) * 3) + 2
 280 J = J - ((J > 39) * (J - 39))
 300 T =  INT ( RND (1) * (4 - N) * 48) + N * 48
 340  FOR Y = T TO 191 STEP 4 - N
 350  HPLOT I * 7,Y TO J * 7 + 6,Y
 390  NEXT 
 395 I = J + 1
 397 M = J = 39
 400  NEXT 
 410  NEXT 

 for narrower buildings:
 240 J = I +  INT ( RND (1) * 2) + 1